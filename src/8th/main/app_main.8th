(*
	app_main.8th
 
  Main application for SDRLibEConsole
 
 Copyright C 2018 by G3UKB Bob Cowdery
 This program is free software; you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation; either version 2 of the License, or
 at your option any later version.
 
  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.
 
  You should have received a copy of the GNU General Public License
  along with this program; if not, write to the Free Software
  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 
  The author can be reached by email at:   
     bob@bobcowdery.plus.com
*)

\ ============================================================================

\ Include all include files
"includes.8th" f:include

\ Make this a gui app
true app:isgui !
\ true log-async

\ Set namespace
ns: sdr

(*
	Entry point for the SDRLibEConsole application. This sets up the entire application
	and then hands control over to the GUI by exiting app:main whereupon the GUI event
	loop is entered. 
*)

\ ----------------------------------
\ Called on application exit
: cleanup
	false log-async
	"Shutdown in progress" log
	\ Close connector
	conn_term 
	\ Close models
	option_model_term
	app_model_term
	\ Close pub/sub
	fr:pubsub_term
	\ Wait for all tasks to terminate
	fr:gs_wait_all
	"Shutdown complete" log
;

\ ----------------------------------
\ We start all gen-server tasks from the startup task
: startup_task
	\ ----------------------------------
	\ Start the gen_server tasks
	\ This has to be done in the correct order as events will
	\ start flying around the system and we want the subscribers
	\ to be ready before the publishers else events will be missed.
	\ Set our task in the task registry
	"STARTUP_TASK" t:name!
	t:curtask "STARTUP_TASK" fr:gs_reg!
	\ Make sure the q does not throw
	t:getq false q:throwing drop
	
	\ ----------------------------------
	\ Allocate param stack for this thread
	param_st_alloc
	
	\ ----------------------------------
	"Creating gen-server tasks..." log
	\ Publish/Subscribe
	\ Initialise
	' do_registrations fr:pubsub_init
	\ Testing only
	\ test_registrations
	
	\ ----------------------------------
	\ Models
	option_model_init
	option_model_ui_evnts
	\ 2 sleep
	app_model_init
	app_model_ui_evnts
		
	\ ----------------------------------
	\ Connector
	\ Initialise
	conn_init
	\ First see if we have a server running by sending it a poll message
	conn_poll if
		\ Try discover to see if we have hardware
		conn_discover "resp" m:@ "ACK" s:= if
			\ We have radio hardware
			"Radio hardware found!" log
			\ Set state to on-line
			true st_online!
		else
			"****" log
			"No radio hardware found!" log
			"To try again use the 'Discovery' button." log
			"****" log
		then
	else
		\ No server or no network connectivity to the server
		"****" log
		"The SDRLibE Server is not running!" log 
		"The application will start and continue to poll for the server." log
		"When a server is detected hardware discovery will run." log
		"****" log
	then
	
	
	"Created gen-server tasks" log
;

\ ===============================================================================
\ Application entry point  
: app:main	\ --

	\ ----------------------------------
	\ Set our task name
	"MainThrd" t:name!
	t:curtask . cr
	
	\ ----------------------------------
	\ Pretty print Json
	\ Note: this messes up save/restore so use only for debug
	\ 2 G:json-pretty
	
	\ Allocate param stack fot main thread
	param_st_alloc
	
	\ ----------------------------------
	\ Create test main window
	\ We must create all windows up front so everything gets populated by the models.
	create_main
	
	\ ----------------------------------
	\ Start the gen_server tasks
	' startup_task t:task t:wait
	\ Wait for the startup task to complete
	\ "STARTUP_TASK" fr:gs_wait_single_task
	
	\ ----------------------------------
  \ Set our cleanup word
  ' cleanup onexit
  
  \ On exit from app:main the GUI event loop is started
  "Exiting app_main" log
;

\ ========================================
\ Reset namespace
ns: user