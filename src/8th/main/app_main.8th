(*
	app_main.8th
 
  Main application for SDRLibEConsole
 
 Copyright C 2018 by G3UKB Bob Cowdery
 This program is free software; you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation; either version 2 of the License, or
 at your option any later version.
 
  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.
 
  You should have received a copy of the GNU General Public License
  along with this program; if not, write to the Free Software
  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 
  The author can be reached by email at:   
     bob@bobcowdery.plus.com
*)

\ ============================================================================

\ Include all include files
"includes.8th" f:include

\ needs debug/trace

\ Make this a gui app
true app:isgui !
true log-async

\ Set namespace
ns: sdr

(*
	Entry point for the SDRLibEConsole application. This sets up the entire application
	and then hands control over to the GUI by exiting app:main whereupon the GUI event
	loop is entered. 
*)
	
\ ----------------------------------
\ We start all gen-server tasks from the startup task
var t_startup
: startup_task	SED: --
	\ in: 	none
	\ out:	none
	\ ----------------------------------
	\ Start the gen_server tasks
	\ This has to be done in the correct order as events will
	\ start flying around the system and we want the subscribers
	\ to be ready before the publishers else events will be missed.
	\ Set our task in the task registry
	"STARTUP_TASK" t:name!
	t:curtask "STARTUP_TASK" fr:gs_reg!
	\ Register the GUI task so we can message it
	t:guitask "MAIN_TASK" fr:gs_reg!
	
	\ Make sure the q does not throw
	t:getq false q:throwing drop
	
	\ ----------------------------------
	\ Allocate param stack for this thread
	param_st_alloc
	
	\ ----------------------------------
	"Creating gen-server tasks..." log
	\ Publish/Subscribe
	\ Initialise
	' do_registrations fr:pubsub_init
	\ Testing only
	\ test_registrations
	
	\ ----------------------------------
	\ Models
	option_model_init
	STATE_2 wait_run_state
	app_model_init
	STATE_3 wait_run_state
	
	\ ----------------------------------
	\ Subscribe propogates down to all widgets which do own subscriptions
	main_window_subscribe 
	options_subscribe
	0.5 sleep
	
	\ ----------------------------------
	\ Publish SYS_RUN_STATE, gets pushed onto main thread for UI to build
	SYS_RUN_STATE [] fr:pubsub_publish
	STATE_4 wait_run_state
	
	\ ----------------------------------
	\ Connector
	\ Initialise
	conn_init
	\ Start the display tasks
	svr_evnt_init
	\ Start the transient task
	con_transient_init
	0.1 sleep
	
	\ ----------------------------------
	\ See if we have a server or simulator running
	conn_poll not if
		"\n\n\***The console cannot run without SDRLibEConnector server or a simulator." log
		"***Please start the server or simulator and then restart the console." log
		"***Press any key to exit the application\n\n" log
		"STARTUP_TASK" fr:gs_reg_rm
		con:key
		bye
	then
	
	\ ----------------------------------
	\ Once UI is stable we can populate it with the UI event data
	option_model_ui_evnts
	STATE_5 wait_run_state
	app_model_ui_evnts
	STATE_6 wait_run_state
	
	\ See if we can discover hardware
	"TRANSIENT_TASK" ' conn_try_connect true fr:msg!
	
	\ ----------------------------------
	\ Done SOD
	false st_app_sod!
	true st_app_run!
	
	\ ----------------------------------
	\ Some UI updates are turned off during SOD
	\ Refresh propogates down the hierarchy
	main_window_refresh
	options_refresh
	
	\ We have started multiple tasks from the startup task
	\ We keep a reference to it and close it at the end of
	\ the shutdown before waiting for all tasks.
	-1 sleep
	
	\ Free the parameter stack
	param_st_free
	
	\ Exit message
	"STARTUP_TASK - exiting..." log ;

\ ----------------------------------
\ We cleanup all gen-server tasks
\ Has to be a separate task so we can reeceive responses
: cleanup_task	SED: --
	\ in: 	none
	\ out:	none
	\ So we know who we are
	"CLEANUP_TASK" t:name!
	\ Otherwise messages are lost in closedown
	false log-async
	\ Allocate param stack for this thread
	param_st_alloc
	
	"Shutdown in progress" log
	\ Close connector and associated tasks
	svr_evnt_term
	conn_term
	conn_transient_term
	
	\ Close models
	option_model_term
	app_model_term
	
	\ Close pub/sub
	fr:pubsub_term
	"Cleanup task - exiting..." log ;
	
\ ----------------------------------
\ Called on application exit
: cleanup	SED: --
	\ in: 	none
	\ out:	none
	\ Otherwise messages are lost in closedown
	false log-async
	\ Run the cleanup task and wait for it to complete
	' cleanup_task t:task t:wait
	\ We have to wait a good time here because
	\ 1. The cleanup task kicks off multiple other tasks to terminat...
	\ 2. The cleanup task exits long before those tasks actually complete.
	\ 3. We have to wait for everything to close down before we can wait for the tasks.
	\ 4. Waiting too early means the wait can take the lock on the task array and then
	\ 	deadlocks the tasks that are trying to close. We should be able to find out
	\		when it is safe but havn't figured that yet.
	1.0 sleep
	\ Finally close the Startup task
	t_startup @ t:notify
	\ Wait for all tasks to terminate
	fr:gs_wait_all
	"Shutdown complete" log
	\ Wait for messages to flush
	0.2 sleep ;

\ ===============================================================================
\ Application entry point  
: app:main	SED: --
	\ in: 	none
	\ out:	none
	
	\ ----------------------------------
	\ Set our task name
	\ "Main_Thrd" t:name!
	"MAIN_TASK" t:name!
	\ Make sure the q does not throw
	t:getq false q:throwing drop
	t:curtask . cr
		
	\ ----------------------------------
	\ Pretty print Json
	\ Note: this messes up save/restore so use only for debug
	\ 2 G:json-pretty
	
	\ Allocate param stack fot main thread
	param_st_alloc
	
	\ ----------------------------------
	\ Initialise main window
	main_window_init
	
	\ ----------------------------------
	\ Init the dialog window
	options_init
	
	\ ----------------------------------
	\ Initialise the Alex window
	alex_window_init
	
	\ ----------------------------------
	\ Start the startup task which starts all gen_server tasks
	\ This cannot itself be a gen-server task as the framework
	\ is not instantiated yet.
	' startup_task t:task dup t_startup !
	
	\ ----------------------------------
  \ Set our cleanup word
  ' cleanup onexit
  
  \ On exit from app:main the GUI event loop is started
  "Exiting app_main" log ;

\ ========================================
\ Reset namespace
ns: user