(*
	general-page.8th
 
  Preference setting dialog for the DynamicSDR application
 
 Copyright C 2017 by G3UKB Bob Cowdery
 This program is free software; you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation; either version 2 of the License, or
 at your option any later version.
 
  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.
 
  You should have received a copy of the GNU General Public License
  along with this program; if not, write to the Free Software
  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 
  The author can be reached by email at:   
     bob@bobcowdery.plus.com
*)

\ Set namespace
ns: sdr

\ ============================================================================
\ General page of the options dialog
{
	"kind" : "box",	
	"bounds" : ` 0 0 25 100 do_bounds `,
	"flex" : ` "col" flex_do_box `,
	"children" :
	[
		{
			"kind" : "label",
			"label" : "Smpl rate",
			"flex" : ` 100 22 flex_do_widget `
		},
		{
			"kind" : "label",
			"label" : "Num RX",
			"flex" : ` 100 22 flex_do_widget `
		},
		{
			"kind" : "label",
			"label" : "Blk sz",
			"flex" : ` 100 22 flex_do_widget `
		}
	]
} var, general_flex_box_labels

{
	"kind" : "box",	
	"bounds" : ` 25 0 100 100 do_bounds `,
	"flex" : ` "col" flex_do_box `,
	"children" :
	[
		{
			"kind" : "combo",
			"name" : "g-sample-rate",
			"editable" : 0,
			"empty-text" : "Please select",
			"no_choices" : "Empty",
			"items" : [],
			"selected" : 0,
			"flex" : ` 100 22 flex_do_widget `,
			"changed" : "sdr:g_sample_rate"
		},
		{
			"kind" : "combo",
			"name" : "g-num-rx",
			"editable" : 0,
			"empty-text" : "Please select",
			"no_choices" : "Empty",
			"items" : [],
			"selected" : 0,
			"flex" : ` 100 22 flex_do_widget `,
			"changed" : "sdr:g_num_rx"
		},
		{
			"kind" : "combo",
			"name" : "g-blk-sz",
			"editable" : 0,
			"empty-text" : "Please select",
			"no_choices" : "Empty",
			"items" : [],
			"selected" : 2,
			"flex" : ` 100 22 flex_do_widget `,
			"changed" : "sdr:g_block_sz"
		}
	]
} var, general_flex_box_widgets

\ Main general options
{
	"label" : "General",
	"kind" : "box",	
	"bounds" : ` 0 8 100 100 do_bounds `,
	"bg" : "cornsilk2",
	"flex" : ` "row" flex_do_box `,
	"children" :
	[
		` general_flex_box_labels @ `,
		` general_flex_box_widgets @ `
	]
} var, general_options
	
\ =====================================================================================================
\ UI Event Handlers
\ These simply update the model (options_model_controllr.8th) which takes any necessary action
: g_sample_rate					\ combo index rate --
	st_app_run@ if
		\ Allow the event
		drop 								\ combo index rate -- combo index
		nip									\ combo index -- index
		\ Update the model
		option_model_set_sample_rate			\ index -- 
	else 
		2drop drop					\ combo index rate --
	then
;

: g_num_rx							\ combo index num-rx --
	st_app_run@ if
		drop swap drop option_model_set_num_rx
	else 2drop drop then
;

: g_block_sz						\ combo index blk-sz --
	st_app_run@ if
		drop swap drop option_model_set_blk_sz
	else 2drop drop then
;

\ ================================================================================	
\ UI Widget Update
\ Helper words in win_common.8th

\ Update words
: gen_evt_sample_rate															\	array-obj-ref value -- array-obj-ref
	"gen_evt_sample_rate" log
	0 a:@ gen_tab "g-sample-rate" update_combo drop \ array_obj_ref value tab "g-sample-rate" -- array-obj-ref
;

: gen_evt_num_rx															\	array-obj-ref value -- array-obj-ref
	"gen_evt_num_rx" log
	0 a:@ gen_tab "g-num-rx" update_combo drop	\ array_obj_ref value tab "g-num-rx" -- array-obj-ref
;

: gen_evt_blk_sz															\	array-obj-ref value -- array-obj-ref
	"gen_evt_blk_sz" log
	0 a:@ gen_tab "g-blk-sz" update_combo drop	\ array_obj_ref value tab "g-blk-sz" -- array-obj-ref
;

: gen_evt_enabletx															\	array-obj-ref value -- array-obj-ref
	"gen_evt_enabletx" log
	0 a:@ gen_tab "g-enabletx" update_toggle drop	\ array_obj_ref value tab "g-enabletx" -- array-obj-ref
;

: opt_gen_subscribe	\ --
  \ Subscribe to events
  UI_SMPL_RATE ' gen_evt_sample_rate fr:pubsub_subscribe
  UI_NUM_RX ' gen_evt_num_rx fr:pubsub_subscribe
  UI_BLK_SZ ' gen_evt_blk_sz fr:pubsub_subscribe
;

\ Restore namespace	
ns: user
	