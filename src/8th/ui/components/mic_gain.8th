(*
	mic-gain.8th
 
  MIC-GAIN slider component for the DynamicSDR application
 
 Copyright C 2018 by G3UKB Bob Cowdery
 This program is free software; you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation; either version 2 of the License, or
 at your option any later version.
 
  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.
 
  You should have received a copy of the GNU General Public License
  along with this program; if not, write to the Free Software
  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 
  The author can be reached by email at:   
     bob@bobcowdery.plus.com
*)

needs stack/rstack
needs stack/utils

\ Use SDR namespace
ns: sdr

\ Be nice to use a new kind but layouts not supported yet 
{
	"kind" : "box",
	"alpha" : 0.5,
	"bg" : "lightcyan4",
	"visible" : false,
	"children" : [
		{
			"kind" : "slider",
			"alpha" : 0.5,
			"bg" : "lightcyan4",
			"min" : 0,
			"max" : 100,
			"interval" : 1,
			"popup-display" : true,
			"value-changed" : "sdr:new_micgain"
		}
	]
} var, micgain_panel

\ ============================================================================
\ GUI Events
\ Set the gain text in the status area
: set_micgain_text							\ parent gain -- parent g.micgain
	1 pick g:parent  							\ parent gain g.radio
	0 g:child 0 g:child						\ parent gain g.container
	"tx-micgain-lbl" g:child 			\ parent gain g.micgain
	swap n:int >s "%" s:+ g:text	\ parent g.micgain
;

\ Set the text after a micgain selection
: micgain_set_text	\ parent micgain id --
	{ "MICGAIN-TX": ' set_micgain_text }
	case
	drop
;

: set_micgain_model
	model_tx_set_micgain
;

: micgain_set_model	\ parent micgain id --
	{"MICGAIN-TX": ' set_micgain_model }
	case
	drop
;

\ GUI event for micgain change
: new_micgain							\ g value --
	\ Get parent
	null -rot	swap					\ null value g 
	dup g:parent 	 					\ null value g parent 
	\ Get the radio tx id and stash in the null hole
	"id" g:m@ 3 swap poke		\ micgain-id value g parent 
	2 pick 4 pick 					\ micgain-id value g parent value micgain-id
	micgain_set_model				\ micgain-id value g parent
	rot											\ micgain-id g parent value
	3 roll									\ g parent value micgain-id
	micgain_set_text 
;

\ ============================================================================
\ Subscriber Event
: micgain_evt_tx_micgain						\ a --
	\ "micgain_evt_tx_micgain" log
 	tx_window @ "tx-box" g:child			\ a tx-box
 	"MICGAIN-TX" g:child swap					\ MIC_GAIN.box a
	"state" m:@ "mic-gain" m:@	nip		\ MIC_GAIN.box a gain-value
	rot 0 g:child swap								\ a gain-slider gain-value
	g:set-value
	2drop
;

\ ============================================================================
\ Create a micgain panel
: micgain_panel_new						\ id bounds -- new-micgain-gui
	micgain_panel @ G:clone nip	\ id bounds micgain-clone
	"id" 3 pick m:!
	"name" 3 pick m:! 
	"bounds" 2 pick m:! 
	nip nip g:new
;

\ Initialise a micgain panel instance
: micgain_panel_init	\  gui -- gui
;

\ Subscribe to mic gain
: micgain_subscribe
	UI_MICGAIN ' micgain_evt_tx_micgain pubsub_subscribe
;

\ Restore namespace	
ns: user	