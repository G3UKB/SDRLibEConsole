(*
	main_window.8th
 
  Main window for the SDRLibEConsole application
 
 Copyright C 2019 by G3UKB Bob Cowdery
 This program is free software; you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation; either version 2 of the License, or
 at your option any later version.
 
  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.
 
  You should have received a copy of the GNU General Public License
  along with this program; if not, write to the Free Software
  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 
  The author can be reached by email at:   
     bob@bobcowdery.plus.com
     
  This component can be multi-instanced to provide a 9 digit VFO that can be
	scrolled with the mouse wheel. It publishes frequency events as it goes.
*)

needs stack/3drop

\ Use SDR namespace
ns: sdr
with: fr.gs
with: fr.ps

\ ----------------------------------
\ Called after first draw
: __ui_initialised
	\ Set UI initialised
	true rst_ui!
;

\ ----------------------------------
\ Main window ref
: get_win
	"win" t:@
;

\ ----------------------------------
\ Box references
: get_main_box
	get_win "main-box" g:child 
;
: get_panel_box
	get_win "panel-box" g:child 
;
: get_settings_box
	get_win "settings-box" g:child 
;

\ ----------------------------------
\ Buttons swap visibility to bring selected gui to top
: __main_button
	get_main_box g:show drop
	get_panel_box g:hide drop
	get_settings_box g:hide drop
;
: __panel_button
	get_main_box g:hide drop
	get_panel_box g:show drop
	get_settings_box g:hide drop
;
: __settings_button
	get_main_box g:hide drop
	get_panel_box g:hide drop
	get_settings_box g:show drop
;

\ ----------------------------------
\ Window definition
: main_window	SED: -- g
	{
		"kind" : "win",
		"title" : "SDRLibEConsole",
		"visible" : true,
		"ontop" : false,
		"bg" : "gray10",
		"resize-corner" : 20,
		"wide" : 800,
		"high" : 400,
		"top" : 100,
		"left" : 100,
		"init" : ' __ui_initialised ,
		"children" : [
			{ 
				"kind" : "box",
				"bounds" : "0,0,parent.width/8,200",
				"children" : [
					{
						"kind" : "box",
						"grid" : {"rows" : 4, "cols" : 1},
						"children" : [
							{
								"kind" : "btn",
								"label" : "MAIN",
								"click" : ' __main_button
							},
							{
								"kind" : "btn",
								"label" : "CONTROL",
								"click" : ' __panel_button
							},
							{
								"kind" : "btn",
								"label" : "SETTINGS",
								"click" : ' __settings_button
							}
						]
					}
				]
			},
			{
				"kind" : "box",
				"name" : "main-box",
				"bg" : "gray10",
				"visible" : true,
				"bounds" : "parent.width/8,0,parent.width,parent.height",
				"children" : []
			},
			{
				"kind" : "box",
				"name" : "panel-box",
				"bg" : "gray10",
				"visible" : false,
				"bounds" : "parent.width/8,0,parent.width,parent.height",
				"children" : []
			},
			{
				"kind" : "box",
				"name" : "settings-box",
				"bg" : "gray10",
				"visible" : false,
				"bounds" : "parent.width/8,0,parent.width,parent.height",
				"children" : []
			}
		]
	} g:new "win" t:!
	
	\ ----------------------------------
	\ Create the fragments and sub-assemblies we will be composing
	\ These are then registered in the GUI cache
	MAIN-CONTROL "30,30,300,100" mc_new
	RADIO-PANEL-1 VFO-1 DISP-1 radio_panel_sub_new
	RADIO-SETTINGS-1 MODE-1 FILT-1 radio_settings_sub_new
	
	\ Add fragments and sub-assemblies to the window
	get_main_box MAIN-CONTROL g_cache_get g:+child drop
	get_panel_box RADIO-PANEL-1 g_cache_get g:+child drop
	get_settings_box RADIO-SETTINGS-1 g_cache_get g:+child drop
;

\ ===============================================================================
\ Restore namespace	
ns: user	
