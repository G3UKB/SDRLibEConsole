(* 
	radio_manager.8th
 
  Manage startup and radio start/stop
 
 Copyright C 2019 by G3UKB Bob Cowdery
 This program is free software; you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation; either version 2 of the License, or
 at your option any later version.
 
  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.
 
  You should have received a copy of the GNU General Public License
  along with this program; if not, write to the Free Software
  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 
  The author can be reached by email at:   
     bob@bobcowdery.plus.com
	
	radio startup determins if the server is running.
		- running - issues start up sequence to make the radio operational.
		- not running - output a message dialog but allow the GUI to continue.
*)

needs gui/msgdlg
\ true dbg:line-info

\ ========================================
\ Testing only
true var, radio_manager_test
radio_manager_test @ #if
	\ Includes
	"E:/Projects/Framework/trunk/src/8th/gen_server.8th" f:include
	"E:/Projects/Framework/trunk/src/8th/pub_sub.8th" f:include
	"../common/app_defs.8th" f:include
	"../common/radio_defs.8th" f:include
	"../common/ps_events.8th" f:include
	"../common/run_state.8th" f:include
	"connector.8th" f:include
	\ needs debug/sed
	\ true SED-CHECK
#then

\ Set namespace
ns: sdr
with: fr.gs
with: fr.ps

\ ===============================================================================
\ PRIVATE

\ ========================================
\ User messages

\ ----------------------------------
\ Dialog response
: __rm_msg_resp	SED: g n --
	\ Just hide the dialog
	drop g:hide
;

\ ----------------------------------
\ Called on main thread to put up warning dialog when server is not found
\ {w,msg} --
: __rm_do_msg	SED: s --
	\ "msg" m:@ nip
	{
		"title" : "Radio Status",
		"top" : 200,
		"left" : 100,
		"high" : 150,
		"wide" : 400,
		"font" : "Arial 14",
		"button-font" : "Arial 12",
		"bg" : "gray",
		"fg" : "red"
		\ "msg" : "SDRLibEConnector server is not running. Please start in order to use the radio"
	}
	"msg" rot m:!
	"parent"  "win" t:@ m:!
	"cb" ' __rm_msg_resp m:!
	g:msgdlg	
;

\ ========================================
\ Response words

\ ----------------------------------
\ Decode response
\ response-map -- true(good)|false(bad)
: __rm_decode_response	SED: m -- T
	"resp" m:@ "ACK" s:= nip if true else false then
;

\ ----------------------------------
\ Response from stop radio
: __rm_stop_radio_response	SED: * --
	__rm_decode_response if
		false rst_running!
	else
		\ Failed to stop radio
		\ { "cb" : ' __rm_do_msg , "msg" : "SDRLibEConnector failed to stop radio!" } g:do
		"SDRLibEConnector failed to stop radio!" __rm_do_msg
		true rst_running!
	then	
;

\ ----------------------------------
\ Response from start radio
: __rm_start_radio_response	SED: * --
	__rm_decode_response if
		true rst_running!
	else
		\ Failed to start radio
		\ { "cb" : ' __rm_do_msg , "msg" : "SDRLibEConnector failed to start radio!" } g:do
		 "SDRLibEConnector failed to start radio!" __rm_do_msg
		false rst_running!
	then	
;

\ ----------------------------------
\ Response from start server
: __rm_server_response	SED: * --
	__rm_decode_response if
		true rst_server!
	else
		\ Failed to start server, we can still run but can't really do very much
		\ { "cb" : ' __rm_do_msg , "msg" : "SDRLibEConnector failed to set default audio route!" } g:do
		"SDRLibEConnector failed to set default audio route!" __rm_do_msg
		false rst_server!
	then	
;

\ ----------------------------------
\ Response from radio discover
: __rm_discover_response	SED: * --
	__rm_decode_response if
		true rst_online!
		CONNECTOR_TASK ' t_conn_start_server [null] ' __rm_server_response +sender msg! response@
	else
		\ No server, we can still run but can't really do very much
		\ { "cb" : ' __rm_do_msg , "msg" : "SDRLibEConnector failed to discover radio hardware!" } g:do
		"SDRLibEConnector failed to discover radio hardware!" __rm_do_msg
		false rst_online!
	then	
;

\ ----------------------------------
\ Response from set default audio route
: __rm_route_response	SED: * --
	__rm_decode_response if
		true rst_audio!
		CONNECTOR_TASK ' t_conn_discover [null] ' __rm_discover_response +sender msg! response@
	else
		\ No server, we can still run but can't really do very much
		\ { "cb" : ' __rm_do_msg , "msg" : "SDRLibEConnector failed to set default audio route!" } g:do
		"SDRLibEConnector failed to set default audio route!" __rm_do_msg
		false rst_audio!
	then	
;

\ ----------------------------------
\ Response from default startup
: __rm_poll_response	SED: * --
	__rm_decode_response if
		true rst_server!
		CONNECTOR_TASK ' t_conn_set_default_route [null] ' __rm_route_response +sender msg! response@
	else
		\ No server, we can still run but can't really do very much
		\ { "cb" : ' __rm_do_msg , "msg" : "SDRLibEConnector server is not running or cannot be contacted!" } g:do
		"SDRLibEConnector server is not running or cannot be contacted!" __rm_do_msg
		false rst_server!
	then	
;
		
\ ===============================================================================
\ PUBLIC

\ ----------------------------------
\ Run the radio start sequence
: rm_start_sequence	SED: --
	
	\ See if we have a server or simulator running
	CONNECTOR_TASK ' t_conn_poll [null] ' __rm_poll_response +sender msg! response@
;

\ ----------------------------------
\ Start radio
: rm_start_radio	SED: --
	
	\ See if we have a server or simulator running
	CONNECTOR_TASK ' t_conn_start_radio [null] ' __rm_start_radio_response +sender msg! response@
;

\ ----------------------------------
\ Stop radio
: rm_stop_radio	SED: --
	
	\ See if we have a server or simulator running
	CONNECTOR_TASK ' t_conn_stop_radio [null] ' __rm_stop_radio_response +sender msg! response@
;

\ ========================================
\ Reset namespace
ns: user

\ ===============================================================================
\ ===============================================================================
\ Testing
\ ===============================================================================
radio_manager_test @ #if

	with: sdr
	with: fr.gs
	with: fr.ps 
	
	true app:isgui !
	4 json-pretty
	
	
	: reg_task
		\ Must register REPL in the task repos to be able to reply
		t:curtask t:name@ reg!
		\ Set q to null on empty
		t:getq false q:throwing \ do not drop q else crashes
	;
	
	: run_test
		\ A minimal window so we can raise dialog
		{
			"kind" : "win",
			"title" : "SDRLibEConsole",
			"visible" : true,
			"ontop" : false,
			"bg" : "gray10",
			"resize-corner" : 20,
			"wide" : 400,
			"high" : 200,
			"top" : 100,
			"left" : 100,
			"children" : []
		} g:new dup "win" t:!

		reg_task
		conn_init
		1 sleep

		rm_start_sequence
		1 sleep
		rst_server@ if
			1 sleep
			rst_online@ if
				rm_start_radio
				"Any key to stop radio" log
				con:key
				rm_stop_radio
				1 sleep
			then
		then
		conn_term
		3 sleep
	;
	
	run_test
	bye
	
#then