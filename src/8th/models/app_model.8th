(*
	app_model.8th
 
  Radio state model for the SDRLibEConsole application
 
 Copyright C 2018 by G3UKB Bob Cowdery
 This program is free software; you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation; either version 2 of the License, or
 at your option any later version.
 
  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.
 
  You should have received a copy of the GNU General Public License
  along with this program; if not, write to the Free Software
  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 
  The author can be reached by email at:   
     bob@bobcowdery.plus.com
*)

needs stack/3drop

\ ========================================
\ Testing only
false var, app_model_test
app_model_test @ #if
	\ Includes```````````````````````````````````````````````````````````````````````````````````````````````````````````
	"../framework/param_st.8th" f:include
	"../framework/gen_server.8th" f:include
	"../framework/struct_manip.8th" f:include
	"../common/events.8th" f:include
	"../common/registrations.8th" f:include
	"../framework/pub_sub.8th"  f:include
	"../testing/test_sink.8th" f:include
	"../common/persist.8th" f:include
	"../common/radio_defs.8th" f:include
	"model_common.8th" f:include
	\ needs debug/sed
	\ true SED-CHECK
#then

\ ========================================

\ Define our SDR namespace
ns: sdr

(*
	GEN_SERVER application -
	Model for application -
		1. The model has a default which is used if there is no persisted model. If there is a persisted
		model this is restored and used as the current model.
		2. The model is saved at end of day and may be saved at other times as a precaution.
		3. This model contains all the data and settings for the radio state. It is used to both populate the
		UI and to set the current values.
*)

\ ===============================================================================
\ PRIVATE
\ Convention: private words start __app_
\ 						message handlers start do_app_
\ ===============================================================================

\ ----------------------------------
\ Default app model structure
\ Model data which is used as default when there is no saved model.
: __app_model_default
	{
		"window-metrics" : {
			"x" : 100,
			"y" : 100,
			"width" : 800,
			"height" : 400
		},
		\ HPSDR Protocol-1 only supports 3 radios
		\ More can be added into the array as required
		"radio-state" : [
			[
				\ src						SoD
				false, [null, false, 
				{
					"display-width": 0,
					"display-height": 0,
					"freq": 7100000,
					"mode": ` CH_LSB ` ,
					"filter": ` CH_2K4 ` ,
					"agc": ` CH_AGC_MED ` ,
					"attn": ` ATTN_NONE ` ,
					"hf-preamp": ` PRE_OFF ` ,
					"6m-preamp": ` PRE_OFF ` ,
					"audio-gain": 50
				} ]
			],
			[
				\ src						SoD
				false, [null, false, 
				{
					"display-width": 0,
					"display-height": 0,
					"freq": 7100000,
					"mode": ` CH_LSB ` ,
					"filter": ` CH_2K4 ` ,
					"agc": ` CH_AGC_MED ` ,
					"attn": ` ATTN_NONE ` ,
					"hf-preamp": ` PRE_OFF ` ,
					"6m-preamp": ` PRE_OFF ` ,
					"audio-gain": 50
				} ]
			],
			[
				\ src						SoD
				false, [null, false, 
				{
					"display-width": 0,
					"display-height": 0,
					"freq": 7100000,
					"mode": ` CH_LSB ` ,
					"filter": ` CH_2K4 ` ,
					"agc": ` CH_AGC_MED ` ,
					"attn": ` ATTN_NONE ` ,
					"hf-preamp": ` PRE_OFF ` ,
					"6m-preamp": ` PRE_OFF ` ,
					"audio-gain": 50
				} ]
			]
			],
			"tx-state" : [
				\ src						SoD
				false, [null, false, 
				{
					"state" :
					{
						"duplex" : false,
						"rf-gain": 50,
						"mic-gain": 50
					},
					\ In simplex mode the TX follows Radio 1 and can't be changed
					"simplex" : 
					{
						\ Copy of the radio 1 values
						"freq": 7100000,
						"mode": ` CH_LSB ` ,
						"filter": ` CH_2K4 `
					},
					\ In duplex mode the TX can be on a different freq/mode/filter than Radio 1
					"duplex" : 
					{
						\ Any allowable combination
						"freq": 7100000,
						"mode": ` CH_LSB ` ,
						"filter": ` CH_2K4 `
					}
				}
			]
		]
	}
;

\ ===============================================================================
\ Model Management

\ ----------------------------------
\ Get model
: __app_model@	SED: -- m
	\ in: 	none
	\ out:	model
	"app_model" t:@ ;

\ ----------------------------------
\ Save model
: __app_model!	SED: m -- 
	\ in: 	model
	\ out:	none
	"app_model" t:! ;
	
\ ----------------------------------
\ Restore model else use the default
: __app_model_restore	SED: --
	\ in: 	none
	\ out:	none
	\ Restore the application model or use default if no saved model
	__app_model_default APP_MODEL_FILENAME restore_model_data __app_model! ;

\ ----------------------------------
\ Save current model 
: __app_model_save	SED: --
	\ in: 	none
	\ out:	none
	__app_model@ APP_MODEL_FILENAME save_model_data drop ;

\ ----------------------------------
\ Send restored/default UI data to all subscribers
: __app_ui_evnts	SED: --
	\ in: 	none
	\ out:	none
	__app_model@ >r
	MAIN_WINDOW_METRICS r@ ["window-metrics"] val@ fr:pubsub_publish 
	UI_DUPLEX r@ ["tx-state", ` dyn_idx ` , ` data_idx ` ] val@ fr:pubsub_publish
	UI_RFGAIN r@ ["tx-state", ` dyn_idx ` , ` data_idx ` ] val@ fr:pubsub_publish
	UI_MICGAIN r@ ["tx-state", ` dyn_idx ` , ` data_idx ` ] val@ fr:pubsub_publish
	UI_R1_FREQ r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R2_FREQ r@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R3_FREQ r@ ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_TX_FREQ r@ ["tx-state", ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R1_MODE r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R2_MODE r@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R3_MODE r@ ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_TX_MODE r@ ["tx-state", ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R1_FILT r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R2_FILT r@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R3_FILT r@ ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_TX_FILT r@ ["tx-state", ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R1_AGC r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R2_AGC r@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R3_AGC r@ ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R_ATTN r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R_HF_PRE r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R_6M_PRE r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R1_AFGAIN r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R2_AFGAIN r@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	UI_R3_AFGAIN r> ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish 
	
	"App sent UI events " log 
;

\ ----------------------------------
\ Send restored/default SYS data to all subscribers
: __app_sys_evnts	SED: --
	\ in: 	none
	\ out:	none
	__app_model@ >r	
	SYS_DUPLEX r@ ["tx-state", ` dyn_idx ` , ` data_idx ` ] val@ fr:pubsub_publish
	SYS_RFGAIN r@ ["tx-state", ` dyn_idx ` , ` data_idx ` ] val@ fr:pubsub_publish
	SYS_MICGAIN r@ ["tx-state", ` dyn_idx ` , ` data_idx ` ] val@ fr:pubsub_publish
	SYS_R1_FREQ r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R2_FREQ r@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R3_FREQ r@ ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_TX_FREQ r@ ["tx-state", ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R1_MODE r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R2_MODE r@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R3_MODE r@ ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish	
	SYS_TX_MODE r@ ["tx-state", ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R1_FILT r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R2_FILT r@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish  
	SYS_R3_FILT r@ ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_TX_FILT r@ ["tx-state", ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R1_AGC r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R2_AGC r@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R3_AGC r@ ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R_ATTN r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R_HF_PRE r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R_6M_PRE r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R1_AFGAIN r@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R2_AFGAIN r@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_R3_AFGAIN r> ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish

	"App sent SYS events " log
;

\ ========================================
\ Message Handlers
\ ========================================

\ Initialise the app model at task level
: do_app_init	SED: m --
	\ in: 	message
	\ out:	none
	drop __app_model_restore
;

\ ----------------------------------
\ Send events
: do_app_ui_evnts	SED: m --
	\ in: 	message
	\ out:	none
	drop __app_ui_evnts
;

: do_app_sys_evnts	SED: m --
	\ in: 	message
	\ out:	none
	drop __app_sys_evnts
;

\ ----------------------------------
\ Close the app model at task level
: do_app_close	SED: m --
	\ in: 	message
	\ out:	none
	drop __app_model_save
;

\ ===============================================================================
\ WINDOW

\ ----------------------------------
\ Set window position and size
: do_app_set_win_metrics	SED: a --
	\ in: 	0: n - y pos
	\				1: n - x pos
	\				2: n - height
	\				3: n - width
	\ out:	none
	a:open
	__app_model@ "window-metrics" m:@ nip
	"height" rot m:!
	"width" rot m:!
	"y" rot m:!
	"x" rot m:! drop ;

\ ----------------------------------
\ Get window position and size
: do_app_get_win_metrics	SED: a --
	\ in: 	sender (task name)
	\ out:	q: a - [x,y,w,h]
	a:open
	__app_model@ "window-metrics" m:@ nip
	"height" m:@ >r  
	"width" m:@ >r 
	"y" m:@ >r 
	"x" m:@ >r
	a:new 0 r> a:! 1 r> a:! 2 r> a:! 3 r> a:!	
	nip fr:response! ;
	
\ ===============================================================================
\ RADIOS

\ ========================================
\ Getters and Setters

\ ========================================
\ Frequency

: do_app_r1_set_freq	SED: a --
	\ in: 	0: n - new R1 frequency in Hz
	\				1: T - true if to be propogated to UI as well as SYS
	\ out: 	none
	a:open
	__app_model@ ["radio-state", ` radio_1 ` , ` dyn_idx ` , ` data_idx ` ] "freq" 4 pick val! 
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "simplex"] "freq" 4 pick val!
	SYS_R1_FREQ __app_model@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	\ SYS_TX_FREQ __app_model@ ["tx-state", ` dyn_idx `, ` data_idx ` ] val@ fr:pubsub_publish
	\ if
	\ 	UI_R1_FREQ __app_model@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	\ 	UI_TX_FREQ __app_model@ ["tx-state", ` dyn_idx `, ` data_idx ` ] val@ fr:pubsub_publish
	\ then 2drop
	2drop
;

\ ----------------------------------
: do_app_r2_set_freq	SED: a --
	a:open
	\ in: 	0: n - new R2 frequency in Hz
	\				1: T - true if to be propogated to UI as well as SYS
	\ out: 	none
	__app_model@ ["radio-state", ` radio_2 ` , ` dyn_idx ` , ` data_idx ` ] "freq" 4 pick val!
	SYS_R2_FREQ __app_model@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	if
		UI_R2_FREQ __app_model@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	then drop
;

\ ----------------------------------
: do_app_r3_set_freq	SED: a --
	a:open
	\ in: 	0: n - new R3 frequency in Hz
	\				1: T - true if to be propogated to UI as well as SYS
	\ out: 	none
	__app_model@ ["radio-state", ` radio_3 ` , ` dyn_idx ` , ` data_idx ` ] "freq" 4 pick val!
	SYS_R3_FREQ __app_model@ ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	if
		UI_R3_FREQ __app_model@ ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	then  drop
;

\ ----------------------------------
: __do_app_tx_set_simplex_freq	SED: n T --
	\ in: 	0: T - true if to be propogated to UI as well as SYS
	\				1: n - new TX simplex frequency in Hz
	\ out: 	none
	swap __app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "simplex"] "freq" 3 pick val!
	SYS_TX_FREQ __app_model@ ["tx-state", ` dyn_idx `, ` data_idx ` ] val@ fr:pubsub_publish
	if
		UI_TX_FREQ __app_model@ ["tx-state", ` dyn_idx `, ` data_idx ` ] val@ fr:pubsub_publish
	then
;

\ ----------------------------------
: __do_app_tx_set_duplex_freq		SED: n T --
	\ in: 	0: T - true if to be propogated to UI as well as SYS
	\				1: n - new TX duplex frequency in Hz
	\ out: 	none
	swap __app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "duplex"] "freq" 3 pick val!
	SYS_TX_FREQ __app_model@ ["tx-state", ` dyn_idx `, ` data_idx ` ] val@ fr:pubsub_publish
	if
		UI_TX_FREQ __app_model@ ["tx-state", ` dyn_idx `, ` data_idx ` ] val@ fr:pubsub_publish
	then
;

\ ----------------------------------
: do_app_tx_set_freq	SED: a --
	a:open
	\ in: 	0: n - new simplex/duplex TX frequency in Hz
	\				1: T - true if to be propogated to UI as well as SYS
	\ out: 	none
	drop ;; __app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "state", "duplex"] val@ if
		__do_app_tx_set_duplex_freq
	else
		__do_app_tx_set_simplex_freq
	then 2drop
;

\ ========================================
\ Modes

\ ----------------------------------
: do_app_r1_set_mode	SED: a --
	a:open
	\ in: 	0: n - new R1 mode
	\ out: 	none
	__app_model@ ["radio-state", ` radio_1 ` , ` dyn_idx ` , ` data_idx ` ] "mode" 3 pick val!
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "simplex"] "mode" 3 pick val!
	SYS_R1_MODE __app_model@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_TX_MODE __app_model@ ["tx-state", ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish drop
;

\ ----------------------------------
: do_app_r2_set_mode	SED: a --
	a:open
	\ in: 	0: n - new R2 mode
	\ out: 	none
	__app_model@ ["radio-state", ` radio_2 ` , ` dyn_idx ` , ` data_idx ` ] "mode" 3 pick val!
	SYS_R2_MODE __app_model@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish drop
;

\ ----------------------------------
: do_app_r3_set_mode	SED: a --
	a:open
	\ in: 	0: n - new R3 mode
	\ out: 	none
	__app_model@ ["radio-state", ` radio_3 ` , ` dyn_idx ` , ` data_idx ` ] "mode" 3 pick val!
	SYS_R3_MODE __app_model@ ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish drop
;

\ ----------------------------------
: __do_app_tx_set_simplex_mode SED: n --
	\ in: 	0: n - new TX simplex mode
	\ out: 	none
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "simplex"] "mode" 3 pick val!
	SYS_TX_MODE __app_model@ ["tx-state", ` dyn_idx `, ` data_idx ` ] val@ fr:pubsub_publish
;

\ ----------------------------------
: __do_app_tx_set_duplex_mode SED: n --
	\ in: 	0: n - new TX duplex mode
	\ out: 	none
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "duplex"] "mode" 3 pick val!
	SYS_TX_MODE __app_model@ ["tx-state", ` dyn_idx `, ` data_idx ` ] val@ fr:pubsub_publish
;

\ ----------------------------------
: do_app_tx_set_mode SED: a --
	a:open
	\ in: 	0: n - new simplex/duplex TX mode
	\ out: 	none
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "state", "duplex"] val@ if
		__do_app_tx_set_duplex_mode
	else
		__do_app_tx_set_simplex_mode
	then drop
;

\ ========================================
\ Filters

: do_app_r1_set_filter SED: a --
	a:open
	\ in: 	0: n - new R1 filter
	\ out: 	none
	__app_model@ ["radio-state", ` radio_1 ` , ` dyn_idx ` , ` data_idx ` ] "filter" 3 pick val!
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "simplex"] "filter" 3 pick val!
	SYS_R1_FILT __app_model@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish
	SYS_TX_FILT __app_model@ ["tx-state", ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish drop
;

\ ----------------------------------
: do_app_r2_set_filter SED: a --
	a:open
	\ in: 	0: n - new R2 filter
	\ out: 	none
	__app_model@ ["radio-state", ` radio_2 ` , ` dyn_idx ` , ` data_idx ` ] "filter" 3 pick val!
	SYS_R2_FILT __app_model@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish drop
;

\ ----------------------------------
: do_app_r3_set_filter SED: a --
	a:open
	\ in: 	0: n - new R2 filter
	\ out: 	none
	__app_model@ ["radio-state", ` radio_3 ` , ` dyn_idx ` , ` data_idx ` ] "filter" 3 pick val!
	SYS_R3_FILT __app_model@ ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish drop
;

\ ----------------------------------
: __do_app_tx_set_simplex_filter SED: n --
	\ in: 	0: n - new TX simplex filter
	\ out: 	none
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "simplex"] "filter" 3 pick val!
	SYS_TX_FILT __app_model@ ["tx-state", ` dyn_idx `, ` data_idx ` ] val@ fr:pubsub_publish
;

\ ----------------------------------
: __do_app_tx_set_duplex_filter SED: n --
	\ in: 	0: n - new TX duplex filter
	\ out: 	none
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "duplex"] "filter" 3 pick val!
	SYS_TX_FILT __app_model@ ["tx-state", ` dyn_idx `, ` data_idx ` ] val@ fr:pubsub_publish
;

\ ----------------------------------
: do_app_tx_set_filter SED: a --
	a:open
	\ in: 	0: n - new simplex/duplex filter
	\ out: 	none
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "state", "duplex"] val@ if
		__do_app_tx_set_duplex_filter
	else
		__do_app_tx_set_simplex_filter
	then drop
;

\ ========================================
\ AGC

: do_app_r1_set_agc SED: a --
	a:open
	\ in: 	0: n - new R1 AGC setting
	\ out: 	none
	__app_model@ ["radio-state", ` radio_1 ` , ` dyn_idx ` , ` data_idx ` ] "agc" 3 pick val!
	SYS_R1_AGC __app_model@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish drop
;

\ ----------------------------------
: do_app_r2_set_agc SED: a --
	a:open
	\ in: 	0: n - new R2 AGC setting
	\ out: 	none
	__app_model@ ["radio-state", ` radio_2 ` , ` dyn_idx ` , ` data_idx ` ] "agc" 3 pick val!
	SYS_R2_AGC __app_model@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish drop
;

\ ----------------------------------
: do_app_r3_set_agc SED: a --
	a:open
	\ in: 	0: n - new R3 AGC setting
	\ out: 	none
	__app_model@ ["radio-state", ` radio_3 ` , ` dyn_idx ` , ` data_idx ` ] "agc" 3 pick val!
	SYS_R3_AGC __app_model@ ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish drop
;

\ ========================================
\ AF Gain

: do_app_r1_set_afgain SED: a --
	a:open
	\ in: 	0: n - new R1 AF gain setting
	\ out: 	none
	__app_model@ ["radio-state", ` radio_1 ` , ` dyn_idx ` , ` data_idx ` ] "audio-gain" 3 pick val!
	SYS_R1_AFGAIN __app_model@ ["radio-state", ` radio_1 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish drop
;

\ ----------------------------------
: do_app_r2_set_afgain SED: a --
	a:open
	\ in: 	0: n - new R2 AF gain setting
	\ out: 	none
	__app_model@ ["radio-state", ` radio_2 ` , ` dyn_idx ` , ` data_idx ` ] "audio-gain" 3 pick val!
	SYS_R2_AFGAIN __app_model@ ["radio-state", ` radio_2 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish drop
;

\ ----------------------------------
: do_app_r3_set_afgain SED: a --
	a:open
	\ in: 	0: n - new R3 AF gain setting
	\ out: 	none
	__app_model@ ["radio-state", ` radio_3 ` , ` dyn_idx ` , ` data_idx ` ] "audio-gain" 3 pick val!
	SYS_R3_AFGAIN __app_model@ ["radio-state", ` radio_3 `, ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish drop
;

\ ============================================
\ Get/Set TX Attributes
\ Duplex
: do_app_tx_set_duplex SED: a --
	a:open
	\ in: 	0: T - true if duplex on
	\ out: 	none
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "state" ] "duplex" 3 pick val! drop
;

\ ----------------------------------
: do_app_tx_get_duplex SED: a --
	a:open
	\ in: 	0: s - sender (task name)
	\ out: 	q: T - true if duplex on
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "state", "duplex"] val@
	fr:response!
;

\ ----------------------------------
: do_app_tx_get_simplex_freq SED: a --
	a:open
	\ in: 	0: s - sender (task name)
	\ out: 	q: n - simplex frequency
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "simplex", "freq"] val@
	fr:response!
;

\ ----------------------------------
: do_app_tx_get_duplex_freq SED: a --
	a:open
	\ in: 	0: s - sender (task name)
	\ out: 	q: n - duplex frequency
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "duplex", "freq"] val@
	fr:response!
;

\ ----------------------------------
: do_app_tx_get_simplex_mode SED: a --
	a:open
	\ in: 	0: s - sender (task name)
	\ out: 	q: n - simplex mode
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "simplex", "mode"] val@
	fr:response!
;

\ ----------------------------------
: do_app_tx_get_duplex_mode SED: a --
	a:open
	\ in: 	0: s - sender (task name)
	\ out: 	q: n - duplex mode
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "duplex", "mode"] val@
	fr:response!
;

\ ----------------------------------
: do_app_tx_get_simplex_filter SED: a --
	a:open
	\ in: 	0: s - sender (task name)
	\ out: 	q: n - simplex filter
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "simplex", "filter"] val@
	fr:response!
;

\ ----------------------------------
: do_app_tx_get_duplex_filter SED: a --
	a:open
	\ in: 	0: s - sender (task name)
	\ out: 	q: n - duplex mode
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "duplex", "filter"] val@
	fr:response!
;

\ ----------------------------------
: do_app_tx_set_rfgain SED: a --
	a:open
	\ in: 	0: n - rf gain value
	\ out: 	none
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "state"] "rf-gain" 3 pick val!
	SYS_RFGAIN __app_model@ ["tx-state", ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish drop
;

\ ----------------------------------
: do_app_tx_set_micgain SED: a --
	a:open
	\ in: 	0: n - mic gain value
	\ out: 	none
	__app_model@ ["tx-state", ` dyn_idx ` , ` data_idx ` , "state"] "mic-gain" 3 pick val!
	SYS_MICGAIN __app_model@ ["tx-state", ` dyn_idx `, ` data_idx `] val@ fr:pubsub_publish drop
;

\ ========================================
\ Internal tests
: do_app_tests	SED: m --
	\ in: 	message
	\ out:	none
	drop 
	\ Tests go here
;

\ ===============================================================================
\ PUBLIC
\ Public procedural interface, call from any thread
\ ===============================================================================

\ ========================================
\ Initialise /destroy
\ ----------------------------------
\ Module initialisation
: app_model_init	SED: --
	\ in: 	none
	\ out:	none
	\ Create the app model gen-server
	"AppModel_TASK" t:curtask 2 ' fr:gen_server t:task-n -1 sleep drop
	\ Ask the model to initialise itself
	"AppModel_TASK" ' do_app_init a:new fr:msg!
	"App model initialised sent" log ;

\ ----------------------------------
\ UI events
: app_model_ui_evnts	SED: --
	"AppModel_TASK" ' do_app_ui_evnts a:new fr:msg! ;

\ ----------------------------------
\ SYS events
: app_model_sys_evnts	SED: --
	"AppModel_TASK" ' do_app_sys_evnts a:new fr:msg! ;
	
\ ----------------------------------
\ Module close
: app_model_term	SED: --
	\ in: 	none
	\ out:	none
	\ Tidy close the model
	"AppModel_TASK" ' do_app_close a:new fr:msg!
	0.5 sleep
	\ Ask the model to exit
	"AppModel_TASK" ' fr:gs_do_term a:new fr:msg! ;

\ ========================================
\ Window functions

\ ----------------------------------
: app_model_set_win_metrics
	\ in: 	0: n - height
	\				1: n - width
	\				2: n - y pos
	\				3: n - x pos
	\ out:	none
	>r >r >r >r
	a:new r> a:push r> a:push r> a:push r> a:push
	"AppModel_TASK" ' do_app_set_win_metrics rot fr:msg! ;

\ ----------------------------------
: app_model_get_win_metrics
	\ in: 	none
	\ out:	[x,y,w,h]
	a:new fr:+sender
	"AppModel_TASK" ' do_app_get_win_metrics rot fr:msg!
	fr:response@ ;
	
\ ========================================
\ Radio functions

\ ========================================
\ Frequency
: app_model_r1_set_freq	SED: n T --
	\ in: 	0: T - true if to be propogated to UI as well as SYS
	\				1: n - new R1 frequency in Hz
	\ out: 	none
	a:new rot a:push swap a:push
	"AppModel_TASK" ' do_app_r1_set_freq rot fr:msg! ;

\ ----------------------------------
: app_model_r2_set_freq	SED: n T --
	\ in: 	0: T - true if to be propogated to UI as well as SYS
	\				1: n - new R2 frequency in Hz
	\ out: 	none
	a:new rot a:push swap a:push
	"AppModel_TASK" ' do_app_r2_set_freq rot fr:msg! ;

\ ----------------------------------	
: app_model_r3_set_freq	SED: n T --
	\ in: 	0: T - true if to be propogated to UI as well as SYS
	\				1: n - new R3 frequency in Hz
	\ out: 	none
	a:new rot a:push swap a:push
	"AppModel_TASK" ' do_app_r3_set_freq rot fr:msg! ;

\ ----------------------------------	
: app_model_tx_set_freq	SED: n T --
	\ in: 	0: T - true if to be propogated to UI as well as SYS
	\				1: n - new TX frequency in Hz
	\ out: 	none
	a:new rot a:push swap a:push
	"AppModel_TASK" ' do_app_tx_set_freq rot fr:msg! ;
	
\ ========================================
\ Modes
: app_model_r1_set_mode	SED: n --
	\ in: 	0: n - new R1 mode
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_r1_set_mode rot fr:msg! ;

\ ----------------------------------	
: app_model_r2_set_mode	SED: n --
	\ in: 	0: n - new R2 mode
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_r2_set_mode rot fr:msg! ;

\ ----------------------------------	
: app_model_r3_set_mode	SED: n --
	\ in: 	0: n - new R3 mode
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_r3_set_mode rot fr:msg! ;

\ ----------------------------------	
: app_model_tx_set_mode	SED: n --
	\ in: 	0: n - new TX mode
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_tx_set_mode rot fr:msg! ;

\ ========================================
\ Filters
: app_model_r1_set_filter	SED: n --
	\ in: 	0: n - new R1 filter
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_r1_set_filter rot fr:msg! ;

\ ----------------------------------
: app_model_r2_set_filter	SED: n --
	\ in: 	0: n - new R2 filter
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_r2_set_filter rot fr:msg! ;

\ ----------------------------------	
: app_model_r3_set_filter	SED: n --
	\ in: 	0: n - new R3 filter
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_r3_set_filter rot fr:msg! ;

\ ----------------------------------	
: app_model_tx_set_filter	SED: n --
	\ in: 	0: n - new TX filter
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_tx_set_filter rot fr:msg! ;

\ ========================================
\ AGC
: app_model_r1_set_agc	SED: n --
	\ in: 	0: n - new R1 AGC
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_r1_set_agc rot fr:msg! ;

\ ----------------------------------
: app_model_r2_set_agc	SED: n --
	\ in: 	0: n - new R2 AGC
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_r2_set_agc rot fr:msg! ;

\ ----------------------------------	
: app_model_r3_set_agc	SED: n --
	\ in: 	0: n - new R3 AGC
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_r3_set_agc rot fr:msg! ;

\ ========================================
\ AF Gain
: app_model_r1_set_afgain	SED: n --
	\ in: 	0: n - new R1 AF gain
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_r1_set_afgain rot fr:msg! ;

\ ----------------------------------	
: app_model_r2_set_afgain	SED: n --
	\ in: 	0: n - new R2 AF gain
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_r2_set_afgain rot fr:msg! ;

\ ----------------------------------	
: app_model_r3_set_afgain	SED: n --
	\ in: 	0: n - new R3 AF gain
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_r3_set_afgain rot fr:msg! ;

\ ========================================
\ TX Attributes
: app_model_tx_set_duplex SED: T --
	\ in: 	0: T - true if duplex
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_tx_set_duplex rot fr:msg! ;

\ ----------------------------------	
: app_model_tx_set_rfgain SED:  n --
	\ in: 	0: n - RF gain
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_tx_set_rfgain rot fr:msg! ;

\ ----------------------------------	
: app_model_tx_set_micgain SED:  n --
	\ in: 	0: n - Mic gain
	\ out: 	none
	a:new swap a:push
	"AppModel_TASK" ' do_app_tx_set_micgain rot fr:msg! ;

\ ----------------------------------		
\ Following are request/response words
: app_model_tx_get_duplex SED:  -- T
	\ in: 	none
	\ out: 	0: T - true if duplex on
	\ Send the parameters plus a reply q
	a:new fr:+sender
	"AppModel_TASK" ' do_app_tx_get_duplex rot fr:msg! 
	fr:response@ ;

\ ----------------------------------	
: app_model_tx_get_simplex_freq SED:  -- n
	\ in: 	none
	\ out: 	0: n - simplex freq
	a:new fr:+sender
	"AppModel_TASK" ' do_app_tx_get_simplex_freq rot fr:msg! 
	fr:response@ ;

\ ----------------------------------	
: app_model_tx_get_duplex_freq SED:  -- n
	\ in: 	none
	\ out: 	0: n - duplex freq
	a:new fr:+sender
	"AppModel_TASK" ' do_app_tx_get_duplex_freq rot fr:msg!
	fr:response@ ;

\ ----------------------------------	
: app_model_tx_get_simplex_mode SED:  -- n
	\ in: 	none
	\ out: 	0: n - simplex mode
	a:new fr:+sender
	"AppModel_TASK" ' do_app_tx_get_simplex_mode rot fr:msg! 
	fr:response@ ;

\ ----------------------------------	
: app_model_tx_get_duplex_mode SED:  -- n
	\ in: 	none
	\ out: 	0: n - duplex mode
	a:new fr:+sender
	"AppModel_TASK" ' do_app_tx_get_duplex_mode rot fr:msg! 
	fr:response@ ;

\ ----------------------------------		
: app_model_tx_get_simplex_filter SED:  -- n
	\ in: 	none
	\ out: 	0: n - simplex filter
	a:new fr:+sender
	"AppModel_TASK" ' do_app_tx_get_simplex_filter rot fr:msg! 
	fr:response@ ;

\ ----------------------------------		
: app_model_tx_get_duplex_filter SED:  -- n
	\ in: 	none
	\ out: 	0: n - duplex filter
	a:new fr:+sender
	"AppModel_TASK" ' do_app_tx_get_duplex_filter rot fr:msg! 
	fr:response@ ;

\ ========================================
\ Restore namespace	
ns: user	

\ ===============================================================================
\ ===============================================================================
\ Testing
\ ===============================================================================
app_model_test @ #if

	\ Common initialisation
	: setup
		"Doing setup" log
		\ Param stack alloc
		param_st_alloc
		\ Pub/sub init
		' sdr:do_registrations fr:pubsub_init
		\ Set up test sink subscriptions
		sdr:test_registrations
		"Setup complete" log
	;
		
	\ Run the procedural tests
	: run_proc
		\ Procedural interface tests
		sdr:app_model_get_win_metrics "Metrics: " swap >s s:+ log
		150 250 750 360 sdr:app_model_set_win_metrics
		sdr:app_model_get_win_metrics "Metrics: " swap >s s:+ log
		7100000 false sdr:app_model_r1_set_freq
		7100000 false sdr:app_model_r2_set_freq
		7100000 false sdr:app_model_r3_set_freq
		7100000 false sdr:app_model_tx_set_freq
		"LSB" sdr:mode_for_name sdr:app_model_r1_set_mode
		"USB" sdr:mode_for_name sdr:app_model_r2_set_mode
		"AM" sdr:mode_for_name sdr:app_model_r3_set_mode
		"FM" sdr:mode_for_name sdr:app_model_tx_set_mode
		"6.0KHz" sdr:filter_for_name sdr:app_model_r1_set_filter
		"4.0KHz" sdr:filter_for_name sdr:app_model_r2_set_filter
		"2.4KHz" sdr:filter_for_name sdr:app_model_r3_set_filter
		"1.0KHz" sdr:filter_for_name sdr:app_model_tx_set_filter
		"Off" sdr:agc_for_name sdr:app_model_r1_set_agc
		"Fast" sdr:agc_for_name sdr:app_model_r2_set_agc
		"Slow" sdr:agc_for_name sdr:app_model_r3_set_agc
		50 sdr:app_model_r1_set_afgain
		60 sdr:app_model_r2_set_afgain
		70 sdr:app_model_r3_set_afgain
		true sdr:app_model_tx_set_duplex
		50 sdr:app_model_tx_set_rfgain
		50 sdr:app_model_tx_set_micgain
		2 sleep
		sdr:app_model_tx_get_duplex "Duplex: " swap >s s:+ log
		sdr:app_model_tx_get_simplex_freq "Simplex freq: " swap >s s:+ log
		sdr:app_model_tx_get_duplex_freq "Duplex freq: " swap >s s:+ log
		sdr:app_model_tx_get_simplex_mode "Simplex mode: " swap >s s:+ log
		sdr:app_model_tx_get_duplex_mode "Duplex mode: " swap >s s:+ log
		sdr:app_model_tx_get_simplex_filter "Simplex filter: " swap >s s:+ log
		sdr:app_model_tx_get_duplex_filter "Duplex filter: " swap >s s:+ log
	;

	\ Run one test iteration
	: runtest
		cr cr "Run test: " swap >s s:+ log
		\ Initialise and do SOD etc
		"Initialising" log
		sdr:app_model_init
		sdr:app_model_ui_evnts
		sdr:app_model_sys_evnts
		"Done initialise" log
		\ Run proc tests
		1 sleep
		"Running proc tests" log
		run_proc
		"Done proc tests" log
		\ Close app model to clean up for next iteration
		"Closing model" log
		sdr:app_model_term 
		"AppModel_TASK" fr:gs_wait_single_task 
		"Done closing model" log
		1 sleep
	;
	
	\ Run tests on a separate thread
	: run_tests
		t:getq false q:throwing drop
		"TEST_TASK" t:name!
		t:curtask "TEST_TASK" fr:gs_reg!
		setup	
		2 sleep
		' runtest 1 5000 loop
		\ Terminate pub/sub
		cr "Closing test..." log
		fr:pubsub_term		
		\ Wait for P/S to terminate
		\ AppModel_TASK already terminated
		"P/S_TASK" fr:gs_wait_single_task 
		"TEST_TASK exiting..." log
	;
	
	\ Test sequence
	"MAIN_TASK" t:name!
	' run_tests t:task
	\ Wait for main task to terminate
	t:wait
	2 sleep
	bye
	
#then
