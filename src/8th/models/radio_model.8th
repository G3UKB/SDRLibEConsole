(* 
	radio_model.8th
 
  Persisted radio state.
 
 Copyright C 2019 by G3UKB Bob Cowdery
 This program is free software; you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation; either version 2 of the License, or
 at your option any later version.
 
  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.
 
  You should have received a copy of the GNU General Public License
  along with this program; if not, write to the Free Software
  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 
  The author can be reached by email at:   
     bob@bobcowdery.plus.com
  
  GEN_SERVER application -
		Model for radio -
			1. The model has a default which is used if there is no persisted model. If there is a persisted
			model this is restored and used as the current model.
			2. The model is saved at end of day and may be saved at other times as a precaution.
			3. This model contains all the data and settings for the radio state. It is used to both populate the
			UI and to set the current values.
*)

\ Set namespace
ns: sdr
with: fr.gs
with: fr.ps
with: fr.sm
with: fr.p

\ ===============================================================================
\ PRIVATE
\ Convention: private words start: 		__conn_
\ 						message handlers start: do_conn_
\							event handlers start: 	__conn_evnt_
\ ===============================================================================

\ ----------------------------------
\ Default app model structure
\ Model data which is used as default when there is no persisted model.
: __radio_model_default
	{
		\ HPSDR Protocol-1 only supports 3 radios
		\ More can be added into the array as required
		"radio-state" : [
			[
				\ src						SoD
				false, [null, false, 
				{
					"freq": 7100000,
					"mode": ` CH_LSB ` ,
					"filter": ` CH_2K4 ` ,
					"agc": ` CH_AGC_MED ` ,
					"attn": ` ATTN_NONE ` ,
					"hf-preamp": ` PRE_OFF ` ,
					"6m-preamp": ` PRE_OFF ` ,
					"audio-gain": 50
				} ]
			],
			[
				\ src						SoD
				false, [null, false, 
				{
					"freq": 7100000,
					"mode": ` CH_LSB ` ,
					"filter": ` CH_2K4 ` ,
					"agc": ` CH_AGC_MED ` ,
					"attn": ` ATTN_NONE ` ,
					"hf-preamp": ` PRE_OFF ` ,
					"6m-preamp": ` PRE_OFF ` ,
					"audio-gain": 50
				} ]
			],
			[
				\ src						SoD
				false, [null, false, 
				{
					"freq": 7100000,
					"mode": ` CH_LSB ` ,
					"filter": ` CH_2K4 ` ,
					"agc": ` CH_AGC_MED ` ,
					"attn": ` ATTN_NONE ` ,
					"hf-preamp": ` PRE_OFF ` ,
					"6m-preamp": ` PRE_OFF ` ,
					"audio-gain": 50
				} ]
			]
			],
			"tx-state" : [
				\ src						SoD
				false, [null, false, 
				{
					"state" :
					{
						"duplex" : false,
						"rf-gain": 50,
						"mic-gain": 50
					},
					\ In simplex mode the TX follows Radio 1 and can't be changed
					"simplex" : 
					{
						\ Copy of the radio 1 values
						"freq": 7100000,
						"mode": ` CH_LSB ` ,
						"filter": ` CH_2K4 `
					},
					\ In duplex mode the TX can be on a different freq/mode/filter than Radio 1
					"duplex" : 
					{
						\ Any allowable combination
						"freq": 7100000,
						"mode": ` CH_LSB ` ,
						"filter": ` CH_2K4 `
					}
				}
			]
		]
	}
;

\ ===============================================================================
\ Model Management

\ ----------------------------------
\ Get model
: __radio_model@	SED: -- m
	\ in: 	none
	\ out:	model
	"radio_model" t:@ 
;

\ ----------------------------------
\ Save model
: __radio_model!	SED: m -- 
	\ in: 	model
	\ out:	none
	"radio_model" t:! 
;
	
\ ----------------------------------
\ Restore model else use the default
: __radio_model_restore	SED: --
	\ in: 	none
	\ out:	none
	\ Restore the application model or use default if no saved model
	__radio_model_default RADIO_MODEL_PATH restore_model_data __radio_model! 
;

\ ----------------------------------
\ Save current model 
: __radio_model_save	SED: --
	\ in: 	none
	\ out:	none
	__radio_model@ RADIO_MODEL_PATH save_model_data drop 
;

\ ===============================================================================
\ Helpers

\ ----------------------------------
\ Response handler
: radio_model_response	SED: * --
	\ log
;

\ ----------------------------------
\ Publish event
: __do_publish	SED:  s a --
	a:new __radio_model@ rot val@ a:push ' radio_model_response +sender swap publish
;

\ ===============================================================================
\ Model updates
: __update_ref_1
	0 a:@ nip >r __radio_model@ ["radio-state", ` radio_1 ` , ` dyn_idx ` , ` data_idx` ]
;
: __update_ref_2
	0 a:@ nip >r __radio_model@ ["radio-state", ` radio_2 ` , ` dyn_idx ` , ` data_idx` ]
;
: __update_ref_3
	0 a:@ nip >r __radio_model@ ["radio-state", ` radio_3 ` , ` dyn_idx ` , ` data_idx` ]
;

\ freq --
: t_radio_r1_freq	SED: n --
	__update_ref_1 "freq" r> val!
;
: t_radio_r2_freq	SED: n --
	__update_ref_2 "freq" r> val!
;
: t_radio_r3_freq	SED: n --
	__update_ref_3 "freq" r> val!
;
\ mode --
: t_radio_r1_mode	SED: n --
	__update_ref_1 "mode" r> val!
;
: t_radio_r2_mode	SED: n --
	__update_ref_2 "mode" r> val!
;
: t_radio_r3_mode	SED: n --
	__update_ref_3 "mode" r> val!
;

\ ===============================================================================
\ Message handlers

\ ========================================
\ Initialisation and termination

\ ----------------------------------
\ Initialise the radio model
: t_radio_init	SED: m --
	\ in: 	message
	\ out:	none
	drop 
	__radio_model_restore
	
	\ Do subscriptions
	t:name@ >r
	r@ ' t_radio_r1_freq SYS_R1_FREQ subscribe
	r@ ' t_radio_r2_freq SYS_R2_FREQ subscribe
	r@ ' t_radio_r3_freq SYS_R3_FREQ subscribe
	r@ ' t_radio_r1_mode SYS_R1_MODE subscribe
	r@ ' t_radio_r2_mode SYS_R2_MODE subscribe
	r> ' t_radio_r3_mode SYS_R3_MODE subscribe
	
	"Radio Model initialised" log
;

\ ----------------------------------
\ Send system events
: t_radio_sys_evnts	SED: m --
	\ in: 	message
	\ out:	none
	drop 
	SYS_R1_FREQ ["radio-state", ` radio_1 ` , ` dyn_idx ` , ` data_idx` ,"freq"] __do_publish drop
	SYS_R1_MODE ["radio-state",` radio_1 ` ,` dyn_idx ` ,` data_idx ` ,"mode"] __do_publish drop
	SYS_R1_FILT ["radio-state",` radio_1 ` ,` dyn_idx ` ,` data_idx ` ,"filter"] __do_publish drop
	"Sent Radio Model SYS events" log
;

\ ----------------------------------
\ Send UI events
: t_radio_ui_evnts	SED: m --
	\ in: 	message
	\ out:	none
	drop
	UI_R1_FREQ ["radio-state", ` radio_1 ` , ` dyn_idx ` , ` data_idx` ,"freq"] __do_publish drop
	UI_R1_MODE ["radio-state",` radio_1 ` ,` dyn_idx ` ,` data_idx ` ,"mode"] __do_publish drop
	UI_R1_FILT ["radio-state",` radio_1 ` ,` dyn_idx ` ,` data_idx ` ,"filter"] __do_publish drop
	"Sent Radio Model UI events" log
;

\ ----------------------------------
\ Close the app model at task level
: t_radio_model_close	SED: m --
	\ in: 	message
	\ out:	none
	drop __radio_model_save
;

\ ===============================================================================
\ ===============================================================================
\ PUBLIC Call level Interface
\ ===============================================================================

\ ===============================================================================
\ Initialisation and termination of gen-server 

\ ----------------------------------
\ Module initialisation
: radio_model_init	SED: --
	\ in: 	none
	\ out:	none
	\ Create the model gen-server
	RADIO_MODEL_TASK gen_server_new
	\ Ask the model to initialise itself
	RADIO_MODEL_TASK ' t_radio_init null msg!
;

\ ----------------------------------
\ System events
: radio_model_sys_events	SED: --
	\ in: 	none
	\ out:	none
	RADIO_MODEL_TASK ' t_radio_sys_evnts null msg!
;

\ ----------------------------------
\ UI events
: radio_model_ui_events	SED: --
	\ in: 	none
	\ out:	none
	RADIO_MODEL_TASK ' t_radio_ui_evnts null msg!
;

\ ----------------------------------
\ Module close
: radio_model_term	SED: --
	\ in: 	none
	\ out:	none
	\ Tidy close the connector
	RADIO_MODEL_TASK ' t_radio_model_close null msg!
	RADIO_MODEL_TASK gen_server_term
	0.1 sleep
;

\ ========================================
\ Reset namespace
ns: user

