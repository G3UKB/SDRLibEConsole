(* 
	task_man.8th
 
  Task management
 
 Copyright C 2018 by G3UKB Bob Cowdery
 This program is free software; you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation; either version 2 of the License, or
 at your option any later version.
 
  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.
 
  You should have received a copy of the GNU General Public License
  along with this program; if not, write to the Free Software
  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 
  The author can be reached by email at:   
     bob@bobcowdery.plus.com
*)

\ ========================================
\ Testing only
\ needs debug/sed
\ true SED-CHECK
\ ========================================

\ Set namespace
ns: fr

\ ===============================================================================
\ Task reference and termination management

\ Holder for task references
{} var, __task_dict

\ Locked var
: locked_task_dict@
	__task_dict lock @ ;

\ Unlocked var	
: unlock_task_dict
	__task_dict unlock drop ;
	
\ Store task referance
: task_reg!	SED: t s --
	\ in: 	t - task object
	\				s - task name
	\ out:	none
	locked_task_dict@
	swap rot  m:! drop 
	unlock_task_dict ;
	
\ Get task reference by name
: task_reg@	SED: s -- t
	\ in: 	s - task name
	\ out:	t - task reference
	locked_task_dict@
	swap m:@ nip 
	unlock_task_dict ;

\ Wait for task to terminate
: wait_task SED: s t --
	\ in:		t - task ref 	(item from dict) 	
	\				s - task name (key from dict)
	\ out:	none
	"Waiting for task: " rot s:+ log
	t:wait ;

\ Wait for all tasks to terminate
: wait_all	SED: --
	locked_task_dict@
	cr ' wait_task m:each 
	unlock_task_dict drop ;

\ ===============================================================================
\ Generic task for implementing messaging passing concurrency 
	
\ ----------------------------------
\ Process 1 item from task Q	
: proc_q	SED: --
	t:pop null? if
		"Warning, p/s q returned null!" log drop
	else
		\ Message to process
		"cb" m:@ w:exec
	then ;
	
\ ----------------------------------
\ Task entry point for generic servers
: gen_server	 SED: --
	\ in: 	given name
	\ out:	none
	\ Assign name and store in registry
	dup t:name! t:curtask swap task_reg!
	
	\ Set us in the run state using a task variable
	true t:name@ "_run" s:+ t:!
	\ Init the param stack for this task
	param_st_alloc
	
	\ Set q to return null on empty
	\ Note, should not happen
	t:getq false q:throwing drop
	\ Wake up main thread which is waiting for us to finish initialisation
	t:main t:notify
	
	\ Loop while run enabled
	repeat
		\ Wait for a wake up notify
		-1 sleep
		' proc_q t:qlen times
		\ Check for exit
		t:name@ "_run" s:+ t:@
	while!
	t:name@ " - exiting..." s:+ log ;

\ ===============================================================================
\ Message send to a given task
: msg!	SED: s w * --
	\ in:		* - opaque data to send 	
	\				w - word to invoke in task
	\				s - task name
	\ out:	none
	\ Push params and copy task name to r
	>p >p >r
	\ Word to invoke under the "cb" key and data under the "data" key
	\ The data is cloned before the send as it must be detached from the current task pool
	m:new "cb" p> m:! "data" p> clone nip m:!
	r@ task_reg@ swap t:push r> task_reg@ t:notify ;
	
\ ========================================
\ Reset namespace
ns: user

\ ===============================================================================
\ Testing
\ ===============================================================================
(*
: tn	\ name t
	swap t:name! 
	sleep
	;


: make_tasks	\ --
	"t1" dup 1 2 ' tn t:task-n swap fr:task_reg! 
	"t2" dup 3 2 ' tn t:task-n swap fr:task_reg!
	"t3" dup 5 2 ' tn t:task-n swap fr:task_reg!
	"t4" dup 7 2 ' tn t:task-n swap fr:task_reg!
	"t5" dup 9 2 ' tn t:task-n swap fr:task_reg!
;
	
make_tasks
fr:wait_all
fr:__task_dict @ . cr
bye
*)	